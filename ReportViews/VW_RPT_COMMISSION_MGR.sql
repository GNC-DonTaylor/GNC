/*
--New NA MANAGER COMMISSION REPORT
SELECT * 
FROM VW_RPT_COMMISSION_MGR (NOLOCK) 
WHERE NATIONALACCOUNT = 'Y'
AND WAREHOUSEID IN ('10','20','40') 
AND KEYREPID IS NOT NULL 
and warehouseid = '10'
and salesrepid = 'S03'--'S04'
and customerid = '10378'--'823560'

---------
AND CUSTOMERGROUP = '518'
ORDER BY 
	COMPANYID,
	WAREHOUSEID,
	SALESREPID,
	CUSTOMERGROUP,
	CUSTOMERID,
	TRANSACTIONNUMBER, 
	TRANSACTIONTYPE
*/
CREATE OR ALTER VIEW [DBO].[VW_RPT_COMMISSION_MGR]
AS
SELECT
	X.*,
	--THIS IS WHERE I SUM UP PAYMENTS BY TRANSACTION: (INVOICES ONLY)
	ISNULL((SELECT SUM(ISNULL(CD.AMOUNTAPPLIED,0)) AS CURRPAYMENT --per Radean, discounttaken is W/O amount to be excluded. 5/25/22 DT
--	ISNULL((SELECT SUM(ISNULL(CD.AMOUNTAPPLIED,0) + ISNULL(CD.DISCOUNTTAKEN,0)) AS CURRPAYMENT
			FROM ARCASHRECEIPTSHEADER CH (NOLOCK) 
			LEFT JOIN ARCASHRECEIPTSDETAIL CD (NOLOCK) ON CD.R_CASHRECEIPTSHEADER = CH.ROWID 
			WHERE CD.R_AROPENITEMHEADER = X.AHROWID
--			AND CD.AMOUNTAPPLIED != 0	--ADDED TO SUPPRESS RECEIPTS WITH NO PAYMENT...WRITE-OFF RECORDS WOULD CAUSE THIS PER RADEAN - 5/27/22 DT
			AND CH.RECORDTYPE <> 'P'
			AND CH.DATERECEIVED BETWEEN X.STARTINGDATE AND X.ENDINGDATE
			--AND X.TRANSACTIONTYPE = 'I'
			)*X.REVSIGN, 0) 
		-	IIF(
				--X.TRANSACTIONTYPE = 'I' AND 
				--ISNULL(X.FREIGHTTOTAL,0.) > ISNULL(X.PREVPAYMENT,0), 
				ISNULL(X.FREIGHTTOTAL,0.)*X.REVSIGN > ISNULL(X.PREVPAYMENT,0)*X.REVSIGN, --REMOVE NEGATIVE VALUES TO APPLY LOGIC OR LOGIC DOESN'T WORK 
				ISNULL(X.FREIGHTTOTAL,0.) - ISNULL(X.PREVPAYMENT,0), 
				0.) 
				AS COMMISSIONQTY 
FROM (
		SELECT
			U.STARTINGDATE,
			U.ENDINGDATE,
			U.FISCALYEAR,
			U.FISCALPERIOD,
			U.FAUXDIVISION,
			U.FAUXREP,
			U.AHROWID,			--USED TO SUM UP PAYMENTS BY TRANSACTION (ABOVE)
			U.RECORDTYPE,
			U.COMPANYID,
			U.COMPANYNAME,
			U.TXT_OPENCLOSED,	--AH FIELD
			U.REVSIGN,			--AH FIELD
			U.CHECKNUMBER,
			U.BATCHTOTAL,
			U.PAYMENTDATE,
		---
			ISNULL(ISNULL(WH.IDENTITYID, CAST (EPLANT.DIVISIONNUMBER AS VARCHAR(12))),U.FAUXDIVISION) AS WAREHOUSEID,
			ISNULL(ISNULL(WH.NAME,		--ARGOS TRANSACTION WAREHOUSE
					(	SELECT eWH.NAME --ePLANT TRANSACTION WAREHOUSE
						FROM IDMASTER eWH (NOLOCK) 
						WHERE eWH.IDENTITYID = CAST (EPLANT.DIVISIONNUMBER AS VARCHAR(12)))
						),
					(	SELECT fWH.NAME --FAUXDIVISION FROM AROPENITEMHEADER AH
						FROM IDMASTER fWH (NOLOCK) 
						WHERE fWH.IDENTITYID = U.FAUXDIVISION
						)
					) AS WAREHOUSENAME,
			ISNULL(ISNULL(PART.IDENTITYID, EPLANT.SALESREPID),U.FAUXREP) AS SALESREPID,
			ISNULL(ISNULL(PART.NAME, EPLANT.SALESREPNAME),(SELECT NAME FROM IDMASTER (NOLOCK) WHERE IDENTITYID = U.FAUXREP)) AS SALESREPNAME,
			KPART.IDENTITYID AS KEYREPID,
			KPART.NAME AS KEYREPNAME,
			ISNULL(ICUST.USERDEFINEDCODE_8,					
					ISNULL(
						(	SELECT IeCUST.USERDEFINEDCODE_8	
							FROM IDMASTER eCONS(NOLOCK)
							JOIN IDRELATIONSHIPLINKS LeCONS (NOLOCK) ON LeCONS.R_IDMEMBER = eCONS.ROWID AND LeCONS.IDMEMBERTYPE = '02'
							JOIN IDMASTER eCUST (NOLOCK) ON eCUST.ROWID = LeCONS.R_IDMASTER
							JOIN IDCUSTOMER IeCUST (NOLOCK) ON IeCUST.R_IDENTITY = eCUST.ROWID
							WHERE eCONS.IDENTITYID = CAST (EPLANT.CUSTOMERNUMBER AS VARCHAR(12)))
					,'N')
					) AS NATIONALACCOUNT,
			ISNULL(ICUST.USERDEFINEDCODE_5,					
					ISNULL(
						(	SELECT IeCUST.USERDEFINEDCODE_5	
							FROM IDMASTER eCONS(NOLOCK)
							JOIN IDRELATIONSHIPLINKS LeCONS (NOLOCK) ON LeCONS.R_IDMEMBER = eCONS.ROWID AND LeCONS.IDMEMBERTYPE = '02'
							JOIN IDMASTER eCUST (NOLOCK) ON eCUST.ROWID = LeCONS.R_IDMASTER
							JOIN IDCUSTOMER IeCUST (NOLOCK) ON IeCUST.R_IDENTITY = eCUST.ROWID
							WHERE eCONS.IDENTITYID = CAST (EPLANT.CUSTOMERNUMBER AS VARCHAR(12)))
					,NULL)
					) AS MAJORCOMMISSIONTYPE,
			ISNULL(CUST.IDGROUP,
					(	SELECT TOP 1 eCONS.IDGROUP 
						FROM IDMASTER eCONS (NOLOCK) 
						WHERE eCONS.IDENTITYID = CAST (EPLANT.CUSTOMERNUMBER AS VARCHAR(12)))
						) AS CUSTOMERGROUP,
			ISNULL(CUST.REFERENCE2,
					(	SELECT eCUST.REFERENCE2
						FROM IDMASTER eCONS(NOLOCK)
						JOIN IDRELATIONSHIPLINKS LeCONS (NOLOCK) ON LeCONS.R_IDMEMBER = eCONS.ROWID AND LeCONS.IDMEMBERTYPE = '02'
						JOIN IDMASTER eCUST (NOLOCK) ON eCUST.ROWID = LeCONS.R_IDMASTER
						WHERE eCONS.IDENTITYID = CAST (EPLANT.CUSTOMERNUMBER AS VARCHAR(12)))
					) AS MAJORGROUPNAME,
			CUST.IDENTITYID AS CUSTOMERID,
			CUST.NAME AS CUSTOMERNAME,
			ISNULL(CONS.IDENTITYID, EPLANT.CUSTOMERNUMBER) AS CONSIGNEEID,
			ISNULL(CONS.NAME, EPLANT.CUSTOMERNAME) AS CONSIGNEENAME,
			'PAYMENT' AS TXT_PAYMENT,
			ISNULL(OH.TRANSACTIONTYPE, IIF(EPLANT.ORDERTYPE = 'C','C','I')) AS TRANSACTIONTYPE,
			IIF(ISNULL(OH.TRANSACTIONTYPE, IIF(EPLANT.ORDERTYPE = 'C','C','I')) = 'I', 'INVOICE', 'CREDIT/DEBIT') AS TXT_INVCREDIT,
			ISNULL(OH.INVOICEDATE, EPLANT.ACTUALSHIPDATE) AS TRANSACTIONDATE,
			ISNULL(ISNULL(OH.TRANSACTIONNUMBER, EPLANT.INVOICENUMBER),U.TRANSACTIONNUMBER) AS TRANSACTIONNUMBER,
			IIF(OH.TRANSACTIONNUMBER IS NULL,
				(	SELECT SUM(AD.AMOUNT) * U.REVSIGN FROM AROPENITEMHEADER AH (NOLOCK)
					JOIN AROPENITEMDETAIL AD (NOLOCK) ON AD.R_AROPENITEM = AH.ROWID
					JOIN ACACCOUNT AC (NOLOCK) ON AC.ROWID = AD.R_GLACCOUNT
					WHERE EPLANT.INVOICENUMBER = AH.TRANSACTIONNUMBER
					--AND AC.ACCOUNT IN ('404','428')),	--MERCH, MISC
					AND AC.ACCOUNT = '428'),	--MISC ONLY FOR ePLANT INVOICES
				DBO.FN_ORDERVALUE(OH.ROWID,NULL) * U.REVSIGN
				) AS ORDERTOTAL,
			IIF(OH.TRANSACTIONNUMBER IS NULL,
				(	SELECT SUM(eFRT.FREIGHT) * U.REVSIGN FROM GNC_GPFREIGHT eFRT (NOLOCK)
					WHERE eFRT.INVOICENUMBER = EPLANT.INVOICENUMBER),
				--(	SELECT SUM(AD.AMOUNT) * U.REVSIGN FROM AROPENITEMHEADER AH (NOLOCK)
				--	JOIN AROPENITEMDETAIL AD (NOLOCK) ON AD.R_AROPENITEM = AH.ROWID
				--	JOIN ACACCOUNT AC (NOLOCK) ON AC.ROWID = AD.R_GLACCOUNT
				--	WHERE EPLANT.INVOICENUMBER = AH.TRANSACTIONNUMBER
				--	AND AC.ACCOUNT = '440'),	--FREIGHT (NO FREIGHT FOR ePLANT RIGHT NOW...CAN WE ENGINEER A SOLUTION?)
				DBO.FN_ORDERVALUE(OH.ROWID,'FREIGHT') * U.REVSIGN
				) AS FREIGHTTOTAL,
			U.PREVPAYMENT * U.REVSIGN AS PREVPAYMENT,
			U.CURRPAYMENT * U.REVSIGN AS CURRPAYMENT

		FROM (
				SELECT
					PP.STARTINGDATE,
					PP.ENDINGDATE,
					PP.FISCALYEAR,
					PP.FISCALPERIOD,
					LEFT(AH.DESCRIPTION,2) AS FAUXDIVISION,							--ePLANT DIVISION OVERRIDE FIELD
					'S'+RIGHT('00'+TRIM(STR(AH.USERDEFINEDCODE_1)),2) AS FAUXREP,	--ePLANT SALESREP OVERRIDE FIELD
					AH.ROWID AS AHROWID,	--USED TO SUM UP PAYMENTS BY TRANSACTION (ABOVE)
					AH.R_CUSTOMER,
					AH.R_SOURCEID,
					AH.SOURCETABLENAME,
					AH.TRANSACTIONNUMBER,
					CH.RECORDTYPE,
					CO.COMPANYID,
					CO.NAME AS COMPANYNAME,
					IIF(AH.RECORDTYPE = 'Z', 'CLOSED', 'OPEN') AS TXT_OPENCLOSED,
					IIF(AH.TRANSACTIONTYPE IN ('A','C'), -1, 1) AS REVSIGN,	--REVERSE THE SIGN FOR ADJUSTMENTS AND CREDITS
					(	
						SELECT TOP 1 CHECKNUMBER FROM ARCASHRECEIPTSPAYMENTS CP (NOLOCK) 
						WHERE CP.R_ARCASHRECEIPTSHEADER = CH.ROWID
						AND CP.CHECKNUMBER IS NOT NULL
					) AS CHECKNUMBER,
					CH.CONTROLAMOUNT AS BATCHTOTAL,
					CH.DATERECEIVED AS PAYMENTDATE,
					(	SELECT SUM(PCD.AMOUNTAPPLIED) --per Radean, discounttaken is W/O amount to be excluded. 5/25/22 DT
--					(	SELECT SUM(PCD.AMOUNTAPPLIED + PCD.DISCOUNTTAKEN) 
						FROM ARCASHRECEIPTSHEADER PCH (NOLOCK) 
						LEFT JOIN ARCASHRECEIPTSDETAIL PCD (NOLOCK) ON PCD.R_CASHRECEIPTSHEADER = PCH.ROWID
						LEFT JOIN AROPENITEMHEADER PAH (NOLOCK) ON AH.ROWID = PCD.R_AROPENITEMHEADER
						WHERE PCH.RECORDTYPE <> 'P'
						AND PAH.TRANSACTIONNUMBER = AH.TRANSACTIONNUMBER
						AND PCH.DATERECEIVED < PP.STARTINGDATE
						) AS PREVPAYMENT,
					(CD.AMOUNTAPPLIED) AS CURRPAYMENT --per Radean, discounttaken is W/O amount to be excluded. 5/25/22 DT
--					(CD.AMOUNTAPPLIED + CD.DISCOUNTTAKEN) AS CURRPAYMENT
				FROM ARCASHRECEIPTSHEADER CH (NOLOCK) 
				LEFT JOIN ARCASHRECEIPTSDETAIL CD (NOLOCK) ON CD.R_CASHRECEIPTSHEADER = CH.ROWID
				LEFT JOIN AROPENITEMHEADER AH (NOLOCK) ON AH.ROWID = CD.R_AROPENITEMHEADER
				LEFT JOIN ABCOMPANY CO (NOLOCK) ON CO.COMPANYID = CH.COMPANYID

				--THIS IS THE MAGICAL DATE TO BASE MONTH END AGING OFF OF:
				, (	SELECT
						PP.FISCALYEAR,
						PP.FISCALPERIOD,
						PP.STARTINGDATE,
						PP.ENDINGDATE + 1 AS ENDINGDATE --Add 1 day so that Date/Time calculations are correct now.
					FROM ACFISCALPERIOD PP (NOLOCK)	--PP IS PREVIOUS FISCAL PERIOD WHICH IS WHAT WE WANT.
					WHERE PP.ENDINGDATE = (	SELECT TOP 1 FP.ENDINGDATE 
											FROM ACFISCALPERIOD FP (NOLOCK)
											WHERE GETDATE() >= FP.ENDINGDATE + 1 --Add 1 day so that Date/Time calculations are correct now.
											ORDER BY FP.ENDINGDATE DESC)
					) PP
				------------------------------------------
				WHERE CH.RECORDTYPE <> 'P'
				AND CH.DATERECEIVED BETWEEN PP.STARTINGDATE AND PP.ENDINGDATE
		) U
		LEFT JOIN GNC_INVCRDB_ALL EPLANT (NOLOCK) ON EPLANT.INVOICENUMBER = U.TRANSACTIONNUMBER
		LEFT JOIN OMTRANSACTIONHEADER OH (NOLOCK) ON OH.ROWID = U.R_SOURCEID AND U.SOURCETABLENAME = 'OMTRANSACTIONHEADER'
		LEFT JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER
		LEFT JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = ISNULL(OH.R_CUSTOMER,U.R_CUSTOMER)	--DEFAULT CUSTOMER FROM AH.R_CUSTOMER FOR PAYMENT RECORDS.
		LEFT JOIN IDMASTER CONS (NOLOCK) ON CONS.ROWID = OH.R_SHIPPER
		LEFT JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = CUST.ROWID
		LEFT JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = CONS.ROWID
		--DO WE NEED TO CONSIDER CONSIGNEE SALESREPIDS WHEN NO REP IS OVERRIDDEN AT THE ORDERHEADER LEVEL?
		--LEFT JOIN IDMASTER PART (NOLOCK) ON PART.ROWID = ISNULL(OH.R_SALESPERSON_1, ICONS.R_PARTICIPANT)
		LEFT JOIN IDMASTER PART (NOLOCK) ON PART.ROWID = OH.R_SALESPERSON_1
		LEFT JOIN IDRELATIONSHIPLINKS LCUST (NOLOCK) ON LCUST.R_IDMASTER = CUST.ROWID AND LCUST.IDMASTERTYPE = '01' AND LCUST.IDMEMBERTYPE = '14'
		LEFT JOIN IDMASTER KPART (NOLOCK) ON KPART.ROWID = LCUST.R_IDMEMBER
		WHERE U.RECORDTYPE <> 'P'
) X
WHERE CURRPAYMENT != 0	--ADDED TO SUPPRESS RECEIPTS WITH NO PAYMENT...WRITE-OFF RECORDS WOULD CAUSE THIS PER RADEAN - 5/27/22 DT



