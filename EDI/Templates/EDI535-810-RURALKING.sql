--EDI810-RURALKING

DECLARE @ROWID UNIQUEIDENTIFIER
SET @ROWID= (	SELECT OH.ROWID FROM OMTRANSACTIONHEADER OH (NOLOCK) 
				JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER 
				WHERE CUST.IDGROUP = '535' AND OH.TRANSACTIONNUMBER = '10-24761-1-21CM'--'10-45775'
			)
------------
--[HEADER]--
------------
SELECT	
	OH.INVOICEDATE, 
	OH.TRANSACTIONDATE, 
	OH.SHIPPINGDATE, 
	OH.TRANSACTIONNUMBER, 
	OH.PURCHASEORDERNUMBER, 
	CASE 
		WHEN OH.TRANSACTIONTYPE = 'J' THEN 
			IIF(ISNULL(DBO.FN_ORDERVALUE(OH.ROWID,NULL),0) < 0, 'DR', 'CN')
		ELSE 'DI'
	END AS TRANSACTIONCODE, 
	WH.IDENTITYID AS WH, 
	CO.NAME AS COMPANYNAME, 
	ISNULL(OH.USERDEFINEDREFERENCE_17,(SELECT VENDOR FROM GNC_EDI_TRANSLATION (NOLOCK) WHERE WAREHOUSEID = WH.IDENTITYID AND IDGROUP = CUST.IDGROUP)) AS PO_VENDOR, 
	CUST.IDENTITYID AS CUSTID, 
	CUST.NAME AS CUSTNAME, 
	CONS.IDENTITYID AS CONSIGNEEID, 
	CONS.NAME AS CONSIGNEENAME, 
	ISNULL(OH.USERDEFINEDREFERENCE_19, ICONS.USERDEFINEDREFERENCE_2) AS PO_STORENUMBER, 
	ISNULL(OH.USERDEFINEDREFERENCE_20, ICONS.USERDEFINEDREFERENCE_6) AS GLNNUMBER, 
	IIF(HEADER.TDSAMT = HEADER.TOTAL, 'X', IIF(HEADER.TDSAMT < HEADER.TOTAL, 'C', 'A')) AS SAC01VALUE, 
	HEADER.TOTAL - HEADER.TDSAMT AS SAC05VALUE, 
	IIF(HEADER.TDSAMT = HEADER.TOTAL, 'X', IIF(HEADER.TDSAMT < HEADER.TOTAL, 'FREIGHT CHARGE', 'FREIGHT ALLOWANCE')) AS SAC15VALUE, 
	HEADER.TDSAMT, HEADER.TOTAL, 
	DETAIL.LINECOUNT 
FROM OMTRANSACTIONHEADER OH (NOLOCK) 
	JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER
	JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER 
	JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = OH.R_CUSTOMER 
	JOIN IDMASTER CONS (NOLOCK) ON CONS.ROWID = OH.R_SHIPPER 
	JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = CONS.ROWID 
	JOIN IDTYPESLINKS (NOLOCK) ON IDTYPESLINKS.R_IDENTITY = CONS.ROWID AND IDTYPESLINKS.IDTYPE = '02' 
	JOIN ABCOMPANY CO (NOLOCK) ON CO.COMPANYID = OH.COMPANYID
	LEFT JOIN	(	SELECT 
						OD.R_TRANSACTIONHEADER, 
						ROUND(SUM(ISNULL(OD.QUANTITYSHIPPED, 0) * 
							(	ISNULL(OD.UNITPRICE, 0) + 
								ISNULL(OD.HANDLINGCHARGEPERITEM,0) + 
								ISNULL(OD.TAGGINGCHARGEPERITEM,0) + 
								IIF(ISNULL(DBO.FN_ORDERFREIGHT(OD.R_TRANSACTIONHEADER, 'FRTZONE'),0) = 0, 0, ISNULL(OD.FREIGHTRATEPERITEM, 0)) 
								--ISNULL(OD.FREIGHTRATEPERITEM,0) 
							) 
						),2) AS TDSAMT, 
						DBO.FN_ORDERVALUE(OD.R_TRANSACTIONHEADER, NULL) AS TOTAL
					FROM OMTRANSACTIONDETAIL OD (NOLOCK) 
					WHERE ISNULL(OD.QUANTITYSHIPPED,0) > 0 
					GROUP BY OD.R_TRANSACTIONHEADER 
				) AS HEADER ON HEADER.R_TRANSACTIONHEADER = OH.ROWID 
	LEFT JOIN	(	SELECT OD.R_TRANSACTIONHEADER, COUNT(*) AS LINECOUNT 
					FROM OMTRANSACTIONDETAIL OD (NOLOCK) 
					WHERE ISNULL(OD.QUANTITYSHIPPED,0) > 0 
					GROUP BY OD.R_TRANSACTIONHEADER 
				) AS DETAIL ON DETAIL.R_TRANSACTIONHEADER = OH.ROWID 
WHERE OH.ROWID = @ROWID AND OH.RECORDTYPE = 'Z' 

------------
--[DETAIL]--
------------
SELECT 
	ITM.DESCRIPTION_1, 
	ITM.ITEMCODE, 
	OD.QUANTITYSHIPPED, 
	CAST(	ISNULL(OD.UNITPRICE, 0) + 
			ISNULL(OD.HANDLINGCHARGEPERITEM,0) + 
			ISNULL(OD.TAGGINGCHARGEPERITEM,0) + 
			IIF(ISNULL(DBO.FN_ORDERFREIGHT(OH.ROWID, 'FRTZONE'),0) = 0, 0, ISNULL(OD.FREIGHTRATEPERITEM, 0)) 
			--ISNULL(OD.FREIGHTRATEPERITEM,0) 
		AS DECIMAL(15,2)) AS LANDEDPRICE, 
	PSD.ALTERNATESKU, 
	ITM.ITEMSUPCCODE 
FROM OMTRANSACTIONHEADER OH (NOLOCK)  
JOIN OMTRANSACTIONDETAIL OD (NOLOCK) ON OD.R_TRANSACTIONHEADER = OH.ROWID 
JOIN IMITEM ITM (NOLOCK) ON ITM.ROWID = OD.R_ITEM 
JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER 
JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = OH.R_CUSTOMER 
JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = OH.R_SHIPPER 
LEFT JOIN IDCONSIGNEESHIPPERZONE SZ (NOLOCK) ON SZ.R_CONSIGNEE = ICONS.R_IDENTITY AND SZ.R_SHIPPER = WH.ROWID 
LEFT JOIN IMPRICESCHEDULEHEADER PSH (NOLOCK) ON PSH.ROWID = ISNULL(SZ.R_PRICESCHEDULE, ICONS.R_PRICESCHEDULE) 
LEFT JOIN IMPRICESCHEDULEDETAIL PSD (NOLOCK) ON PSD.R_PRICESCHEDULEHEADER = PSH.ROWID AND PSD.R_ITEM = OD.R_ITEM AND PSD.EXPIRATIONDATE >= GETDATE() 
WHERE ISNULL(OD.QUANTITYSHIPPED,0) > 0 
AND OH.ROWID = @ROWID 
ORDER BY OD.KEYSEQUENCE 
