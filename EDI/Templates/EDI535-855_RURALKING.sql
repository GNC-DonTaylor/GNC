--EDI855_RURALKING

DECLARE @ROWID UNIQUEIDENTIFIER
SET @ROWID= (	SELECT OH.ROWID FROM OMTRANSACTIONHEADER OH (NOLOCK) 
				JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER 
				WHERE CUST.IDGROUP = '535' AND OH.TRANSACTIONNUMBER = '10-53753'
			)
------------
--[HEADER]--
------------
SELECT	
	OH.TRANSACTIONNUMBER, OH.RECORDTYPE, 
	CASE 
		WHEN OH.STAGE = 'L' THEN 'RJ' 
		WHEN DBO.FN_EDI_PO_CHANGES(OH.TRANSACTIONNUMBER, NULL, 'O') > 0 THEN 'AC'
		ELSE 'AD'
	END AS ACKNOWLEGMENTTYPE, 
	OH.PURCHASEORDERNUMBER, 
	ISNULL(OH.COMMITDATE, OH.TRANSACTIONDATE) AS COMMITDATE, 
	OH.TRANSACTIONDATE, 
	OH.SHIPPINGDATE, 
	(	SELECT TOP 1 POH.USERDEFINEDDATE_19
		FROM OMTRANSACTIONHEADER POH (NOLOCK) 
		WHERE POH.PURCHASEORDERNUMBER = OH.PURCHASEORDERNUMBER 
		AND POH.RECORDTYPE IN('I', 'E')
		ORDER BY POH.RECORDTYPE
	) AS SHIPNOTBEFORE, 
	(	SELECT TOP 1 POH.USERDEFINEDDATE_20
		FROM OMTRANSACTIONHEADER POH (NOLOCK) 
		WHERE POH.PURCHASEORDERNUMBER = OH.PURCHASEORDERNUMBER 
		AND POH.RECORDTYPE IN('I', 'E')
		ORDER BY POH.RECORDTYPE
	) AS SHIPNOTAFTER, 
	EDI.VENDOR AS PO_VENDOR, 
	WH.IDENTITYID, 
	CONS.IDGROUP,
	CONS.NAME AS CONSIGNEENAME, 
	ISNULL(OH.USERDEFINEDREFERENCE_19, ICONS.USERDEFINEDREFERENCE_2) AS PO_STORENUMBER, 
	VCONS.CONSIGNEEADDRESS_1 AS SHIPTOADDRESS1, 
	VCONS.CONSIGNEEADDRESS_2 AS SHIPTOADDRESS2, 
	VCONS.CONSIGNEECITY AS SHIPTOCITY, 
	VCONS.CONSIGNEESTATE AS SHIPTOSTATE, 
	VCONS.CONSIGNEEZIP AS SHIPTOZIP, 
	ISNULL(VCONS.CONSIGNEECOUNTRY, 'US') AS SHIPTOCOUNTRY
FROM OMTRANSACTIONHEADER OH (NOLOCK)
JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER 
JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER
JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = CUST.ROWID
JOIN IDMASTER CONS (NOLOCK) ON CONS.ROWID = OH.R_SHIPPER 
JOIN VW_IDCONSIGNEEVIEW VCONS (NOLOCK) ON VCONS.CONSIGNEEMASTERROWID = OH.R_SHIPPER
JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = OH.R_SHIPPER 
JOIN GNC_EDI_TRANSLATION EDI (NOLOCK) ON EDI.IDGROUP = CUST.IDGROUP AND EDI.WAREHOUSEID = WH.IDENTITYID
WHERE OH.ROWID = @ROWID 
AND OH.TRANSACTIONTYPE = 'I'			--INVOICES ONLY
AND ISNULL(OH.INVENTORYREVIEW,'') <> 'Y'--NOT IN INVENTORYREVIEW

------------
--[DETAIL]--
------------
SELECT	
	DBO.FN_EDI_PO_CHANGES(OH.TRANSACTIONNUMBER, OD.LINENUMBER, 'P') AS PRICECHANGE,
	DBO.FN_EDI_PO_CHANGES(OH.TRANSACTIONNUMBER, OD.LINENUMBER, 'Q') AS QUANTITYCHANGE,
	IIF(DBO.FN_EDI_PO_CHANGES(OH.TRANSACTIONNUMBER, OD.LINENUMBER, 'P') > 0, 'IP', '') AS IPACK,
	IIF(DBO.FN_EDI_PO_CHANGES(OH.TRANSACTIONNUMBER, OD.LINENUMBER, 'Q') > 0, 'IQ', '') AS IQACK,
	IIF(OH.STAGE = 'L', 'IR', IIF(DBO.FN_EDI_PO_CHANGES(OH.TRANSACTIONNUMBER, OD.LINENUMBER, 'O') = 0, 'IA', 'XX')) AS IAACK,
	ISNULL(OD.UNITOFMEASURE,'EA') AS UNITOFMEASURE,
	OD.LINENUMBER, 
	IIF(CHARINDEX(OH.STAGE,'A;S;C')>0, ISNULL(OD.QUANTITYSHIPPED,0), ISNULL(OD.QUANTITYORDERED,0)) AS QUANTITYATSTAGE,
	(	CAST(OD.UNITPRICE AS DECIMAL(15,2)) + 
		ISNULL(OD.HANDLINGCHARGEPERITEM,0) + 
		ISNULL(OD.TAGGINGCHARGEPERITEM,0) + 
		IIF(ISNULL(DBO.FN_ORDERFREIGHT(OH.ROWID, 'FRTZONE'),0) = 0, 0, ISNULL(OD.FREIGHTRATEPERITEM, 0)) 
		--ISNULL(OD.FREIGHTRATEPERITEM,0) 
	) AS LANDEDPRICE, 
	PSD.ALTERNATESKU AS CUSTOMERSKU 
FROM OMTRANSACTIONDETAIL OD (NOLOCK) 
JOIN OMTRANSACTIONHEADER OH (NOLOCK) ON OH.ROWID = OD.R_TRANSACTIONHEADER 
JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER 
JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = OH.R_CUSTOMER 
JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = OH.R_SHIPPER 
LEFT JOIN IDCONSIGNEESHIPPERZONE SZ (NOLOCK) ON SZ.R_CONSIGNEE = ICONS.R_IDENTITY AND SZ.R_SHIPPER = WH.ROWID 
LEFT JOIN IMPRICESCHEDULEHEADER PSH (NOLOCK) ON PSH.ROWID = ISNULL(SZ.R_PRICESCHEDULE, ICONS.R_PRICESCHEDULE) 
LEFT JOIN IMPRICESCHEDULEDETAIL PSD (NOLOCK) ON PSD.R_PRICESCHEDULEHEADER = PSH.ROWID AND PSD.R_ITEM = OD.R_ITEM AND PSD.EXPIRATIONDATE >= GETDATE() 
WHERE (OD.R_TRANSACTIONHEADER = @ROWID) AND (ISNULL(OD.QUANTITYORDERED,0) > 0) 
AND OH.TRANSACTIONTYPE = 'I'			--INVOICES ONLY
AND ISNULL(OH.INVENTORYREVIEW,'') <> 'Y'--NOT IN INVENTORYREVIEW
ORDER BY OD.LINENUMBER ASC 
