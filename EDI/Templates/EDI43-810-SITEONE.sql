--EDI810-SITEONE
/*
SELECT * FROM IDMASTER CUST (NOLOCK) WHERE NAME LIKE '%SITE%' ORDER BY IDENTITYID
SELECT wh.identityid, 
	OH.TRANSACTIONNUMBER, OH.CREATIONDATETIME, OH.STAGE, OH.RECORDTYPE, 
	PSH.PRICESCHEDULECODE
	FROM OMTRANSACTIONHEADER OH (NOLOCK) 
	JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER 
	JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER 
	JOIN IDMASTER CONS (NOLOCK) ON CONS.ROWID = OH.R_SHIPPER 
	JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = CONS.ROWID 
	LEFT JOIN IDCONSIGNEESHIPPERZONE SZ (NOLOCK) ON SZ.R_CONSIGNEE = CONS.ROWID AND SZ.R_SHIPPER = WH.ROWID
	LEFT JOIN IMPRICESCHEDULEHEADER PSH (NOLOCK) ON PSH.ROWID = ISNULL(SZ.R_PRICESCHEDULE, ICONS.R_PRICESCHEDULE)
WHERE CUST.IDGROUP = '43'
ORDER BY OH.CREATIONDATETIME, OH.RECORDTYPE DESC, OH.STAGE, OH.TRANSACTIONNUMBER

SELECT * FROM IMPRICESCHEDULEHEADER PSH (NOLOCK) WHERE PSH.PRICESCHEDULECODE LIKE '%SITE%'

*/
DECLARE @ROWID UNIQUEIDENTIFIER
SET @ROWID= (	SELECT OH.ROWID FROM OMTRANSACTIONHEADER OH (NOLOCK) 
				JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER 
				WHERE CUST.IDGROUP = '43' AND OH.TRANSACTIONNUMBER = 
				--'20-93724'--missing sku
				'10-93220'--invalid po# s/b 42466042
			)
------------
--[HEADER]-- 
------------
SELECT	
	OH.INVOICEDATE, 
	--OH.TRANSACTIONDATE, 
	ISNULL(POH.TRANSACTIONDATE, OH.TRANSACTIONDATE) AS PODATE, 
	OH.SHIPPINGDATE, 
	OH.TRANSACTIONNUMBER, 
	OH.PURCHASEORDERNUMBER, 
	CASE 
		WHEN OH.TRANSACTIONTYPE = 'J' THEN 
			IIF(ISNULL(DBO.FN_ORDERVALUE(OH.ROWID,NULL),0) < 0, 'DR', 'CN')
		ELSE 'DI'
	END AS TRANSACTIONCODE, 
	WH.IDENTITYID AS WH, 
	CO.NAME AS COMPANYNAME, 
	ISNULL(OH.USERDEFINEDREFERENCE_17,(SELECT VENDOR FROM GNC_EDI_TRANSLATION (NOLOCK) WHERE WAREHOUSEID = WH.IDENTITYID AND IDGROUP = CUST.IDGROUP)) AS PO_VENDOR, 
	CUST.IDENTITYID AS CUSTID, 
	CUST.NAME AS CUSTNAME, 
	CONS.IDENTITYID AS CONSIGNEEID, (SELECT IDENTITYID FROM IDMASTER ALTCONS (NOLOCK) WHERE ALTCONS.ROWID = ALTICONS.R_IDENTITY) AS ALTCONSIGNEEID, 
	--SITE ONE ISN'T AMUSED WITH THE 'ALTSHIP' LABEL
	ISNULL(REPLACE(REPLACE(ALTSHIP.NAME,'ALTSHIP ',''),'.',''), CONS.NAME) AS CONSIGNEENAME, 
	--SITE ONE DOESN'T WANT THE ALTSHIP NAME IN THE ADDRESS EITHER 
	CASE
		WHEN LEFT(ALTSHIP.ADDRESS_1,7)  = 'ALTSHIP' THEN ALTSHIP.ADDRESS_2 --USE ADD_2 INSTEAD OF ADD_1 FOR ALTSHIPS 
		ELSE ACONS.ADDRESS_1
	END AS CONSIGNEEADDRESS_1, 
	CASE
		WHEN LEFT(ALTSHIP.ADDRESS_1,7)  = 'ALTSHIP' THEN '' ----SEND NULL FOR ADD_2 FOR ALTSHIPS 
		ELSE ACONS.ADDRESS_2
	END AS CONSIGNEEADDRESS_2, 
	--ISNULL(ALTSHIP.ADDRESS_2, ACONS.ADDRESS_2) AS CONSIGNEEADDRESS_2, 
	ISNULL(ALTSHIP.CITY, ACONS.CITY) AS CONSIGNEECITY, 
	ISNULL(ALTSHIP.STATE, ACONS.STATE) AS CONSIGNEESTATE, 
	ISNULL(ALTSHIP.ZIP, ACONS.ZIP) AS CONSIGNEEZIP, 
	--OVERLOAD ALTSHIPTO_STORE# THEN PO_STORE# THEN CONSIGNEE STORE#:
	ISNULL(ALTICONS.USERDEFINEDREFERENCE_2,ISNULL(OH.USERDEFINEDREFERENCE_19, ICONS.USERDEFINEDREFERENCE_2)) AS PO_STORENUMBER, 
	ISNULL(OH.USERDEFINEDREFERENCE_20, ICONS.USERDEFINEDREFERENCE_6) AS GLNNUMBER, 
--Changed this in the map because we don't always use ZoneFreight and it wasn't being included in the SAC segment...
	IIF((HEADER.TOTAL - HEADER.MERCHAMT) = 0, 'X', IIF((HEADER.TOTAL - HEADER.MERCHAMT) > 0, 'C', 'A')) AS SAC01VALUE, 
	(HEADER.TOTAL - HEADER.MERCHAMT) AS SAC05VALUE, 
	IIF((HEADER.TOTAL - HEADER.MERCHAMT) = 0, 'X', IIF((HEADER.TOTAL - HEADER.MERCHAMT) > 0, 'FREIGHT CHARGE', 'FREIGHT ALLOWANCE')) AS SAC15VALUE, 
--How it used to read:
	--IIF(HEADER.SACAMT = 0, 'X', IIF(HEADER.SACAMT > 0, 'C', 'A')) AS SAC01VALUE, 
	--HEADER.SACAMT AS SAC05VALUE, 
	--IIF(HEADER.SACAMT = 0, 'X', IIF(HEADER.SACAMT > 0, 'FREIGHT CHARGE', 'FREIGHT ALLOWANCE')) AS SAC15VALUE, 
	TERMS.DUEDATEDAY AS ITD07VALUE, TERMS.STATEMENTCOMMENT AS ITD12VALUE, 
	HEADER.MERCHAMT, HEADER.TOTAL, DETAIL.LINECOUNT 
FROM OMTRANSACTIONHEADER OH (NOLOCK) 
	LEFT JOIN OMTRANSACTIONHEADER POH (NOLOCK) ON POH.PURCHASEORDERNUMBER = OH.PURCHASEORDERNUMBER AND POH.RECORDTYPE = 'I' 
	JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER
	JOIN IDMASTER CUST (NOLOCK) ON CUST.ROWID = OH.R_CUSTOMER 
	JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = OH.R_CUSTOMER 
	JOIN IDMASTER CONS (NOLOCK) ON CONS.ROWID = OH.R_SHIPPER 
	JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = CONS.ROWID 
	--must include tcon link to get proper address, if customer is a mastercustomer.
	JOIN IDTYPESLINKS TCONS (NOLOCK) ON TCONS.R_IDENTITY = CONS.ROWID AND TCONS.IDTYPE = '02'
	JOIN IDADDRESS ACONS (NOLOCK) ON ACONS.ROWID = ISNULL(TCONS.R_ADDRESS, CONS.R_DEFAULTADDRESS)

	--TO GET TO THE ALTERNATE STORENUMBER IN IDCONSIGNEE TABLE: (A LINK WHERE ONE DOESN'T EXIST)
	LEFT JOIN IDSYNONYMS ALTSHIP (NOLOCK) ON ALTSHIP.ROWID = OH.R_SYNONYMSALTSHIPTO 
	--	LEFT JOIN IDADDRESSES THAT MATCH THE ALTSHIP.NAME (NO ROWIDS LINKING THIS POINT, ALTSHIPNAME ONLY):
		LEFT JOIN IDADDRESS ALTACONS (NOLOCK) ON ALTACONS.ADDRESS_1 = ALTSHIP.NAME 
	--	LEFT JOIN IDCONSIGNEES THAT MATCH THE RECORD THE ADDRESS IS POINTING TO:
		LEFT JOIN IDCONSIGNEE ALTICONS (NOLOCK) ON ALTICONS.R_IDENTITY = ALTACONS.R_IDENTITY 
	LEFT JOIN IDTERMS TERMS (NOLOCK) ON TERMS.ROWID = OH.R_TERMS
	JOIN ABCOMPANY CO (NOLOCK) ON CO.COMPANYID = OH.COMPANYID
	LEFT JOIN	(	SELECT 
						OD.R_TRANSACTIONHEADER, 
						ROUND(SUM(ISNULL(OD.QUANTITYSHIPPED, 0) * 
							(	ISNULL(OD.UNITPRICE, 0) + 
								ISNULL(OD.HANDLINGCHARGEPERITEM,0) + 
								ISNULL(OD.TAGGINGCHARGEPERITEM,0) --+ 
								--IIF(ISNULL(DBO.FN_ORDERFREIGHT(OD.R_TRANSACTIONHEADER, 'FRTZONE'),0) = 0, 0, ISNULL(OD.FREIGHTRATEPERITEM, 0)) 
								--ISNULL(OD.FREIGHTRATEPERITEM,0) 
							) 
						),2) AS MERCHAMT, 
						ROUND(SUM(ISNULL(OD.QUANTITYSHIPPED, 0) * 
							(	IIF(ISNULL(DBO.FN_ORDERFREIGHT(OD.R_TRANSACTIONHEADER, 'FRTZONE'),0) = 0, 0, ISNULL(OD.FREIGHTRATEPERITEM, 0)) 
								--ISNULL(OD.FREIGHTRATEPERITEM,0) 
							) 
						),2) AS SACAMT, 
						DBO.FN_ORDERVALUE(OD.R_TRANSACTIONHEADER, NULL) AS TOTAL
					FROM OMTRANSACTIONDETAIL OD (NOLOCK) 
					WHERE ISNULL(OD.QUANTITYSHIPPED,0) > 0 
					GROUP BY OD.R_TRANSACTIONHEADER 
				) AS HEADER ON HEADER.R_TRANSACTIONHEADER = OH.ROWID 
	LEFT JOIN	(	SELECT OD.R_TRANSACTIONHEADER, COUNT(*) AS LINECOUNT 
					FROM OMTRANSACTIONDETAIL OD (NOLOCK) 
					WHERE ISNULL(OD.QUANTITYSHIPPED,0) > 0 
					GROUP BY OD.R_TRANSACTIONHEADER 
				) AS DETAIL ON DETAIL.R_TRANSACTIONHEADER = OH.ROWID 
WHERE OH.ROWID = @ROWID AND OH.TRANSACTIONTYPE = 'I' AND OH.RECORDTYPE = 'Z' 

------------
--[DETAIL]--
------------
SELECT 
	ISNULL(POD.LINENUMBER,OD.LINENUMBER) AS LINENUMBER, 
	ITM.DESCRIPTION_1, 
	ITM.ITEMCODE, 
	OD.QUANTITYSHIPPED, 
	ISNULL(POD.UNITOFMEASURE, ISNULL(OD.UNITOFMEASURE, 'EA')) AS UNITOFMEASURE, 
	CAST(	ISNULL(OD.UNITPRICE, 0) + 
			ISNULL(OD.HANDLINGCHARGEPERITEM,0) + 
			ISNULL(OD.TAGGINGCHARGEPERITEM,0) 
		AS DECIMAL(15,2)) AS ITEMPRICE, 
	CAST(	ISNULL(OD.UNITPRICE, 0) + 
			ISNULL(OD.HANDLINGCHARGEPERITEM,0) + 
			ISNULL(OD.TAGGINGCHARGEPERITEM,0) + 
			IIF(ISNULL(DBO.FN_ORDERFREIGHT(OH.ROWID, 'FRTZONE'),0) = 0, 0, ISNULL(OD.FREIGHTRATEPERITEM, 0)) 
			--ISNULL(OD.FREIGHTRATEPERITEM,0) 
		AS DECIMAL(15,2)) AS LANDEDPRICE, 
	PSD.ALTERNATESKU, 
	ITM.ITEMSUPCCODE 
FROM OMTRANSACTIONHEADER OH (NOLOCK)  
JOIN OMTRANSACTIONDETAIL OD (NOLOCK) ON OD.R_TRANSACTIONHEADER = OH.ROWID 
LEFT JOIN OMTRANSACTIONHEADER POH (NOLOCK) ON POH.PURCHASEORDERNUMBER = OH.PURCHASEORDERNUMBER AND POH.RECORDTYPE = 'I' 
LEFT JOIN OMTRANSACTIONDETAIL POD (NOLOCK) ON POD.R_TRANSACTIONHEADER = POH.ROWID AND POD.LINENUMBER = OD.LINENUMBER
JOIN IMITEM ITM (NOLOCK) ON ITM.ROWID = OD.R_ITEM 
JOIN IDMASTER WH (NOLOCK) ON WH.ROWID = OH.R_FMSHIPPER 
JOIN IDCUSTOMER ICUST (NOLOCK) ON ICUST.R_IDENTITY = OH.R_CUSTOMER 
JOIN IDCONSIGNEE ICONS (NOLOCK) ON ICONS.R_IDENTITY = OH.R_SHIPPER 
LEFT JOIN IDCONSIGNEESHIPPERZONE SZ (NOLOCK) ON SZ.R_CONSIGNEE = ICONS.R_IDENTITY AND SZ.R_SHIPPER = WH.ROWID 
LEFT JOIN IMPRICESCHEDULEHEADER PSH (NOLOCK) ON PSH.ROWID = ISNULL(SZ.R_PRICESCHEDULE, ICONS.R_PRICESCHEDULE) 
LEFT JOIN IMPRICESCHEDULEDETAIL PSD (NOLOCK) ON PSD.R_PRICESCHEDULEHEADER = PSH.ROWID AND PSD.R_ITEM = OD.R_ITEM AND PSD.EXPIRATIONDATE >= GETDATE() 
WHERE ISNULL(OD.QUANTITYSHIPPED,0) > 0 
AND OH.ROWID = @ROWID AND OH.TRANSACTIONTYPE = 'I' AND OH.RECORDTYPE = 'Z' 
ORDER BY OD.KEYSEQUENCE 
